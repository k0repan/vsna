@echo off
setlocal enabledelayedexpansion

set "SCRIPT_DIR=%~dp0"
pushd "%SCRIPT_DIR%"

set "OUT_DIR=%OUT_DIR%"
if "%OUT_DIR%"=="" set "OUT_DIR=out"

set "VCPKG_DIR=%SCRIPT_DIR%\vcpkg"
set "TRIPLET=x64-windows"
set "USE_VCPKG=true"
goto parse_loop

:usage
echo Usage: %~nx0 ^<command^>
echo Commands:
echo   --no-vcpkg  Configure and build without vcpkg (via lib-devel)
echo   --clean     Remove build directory
exit /b 0


:cmd_build
set "BUILD_DIR=%OUT_DIR%"
if not exist "%BUILD_DIR%" md "%BUILD_DIR%"

if "%USE_VCPKG%"=="true" (
    if not exist "%VCPKG_DIR%" (
        echo -- Error: vcpkg not found. Run ./init_modules.sh
        exit /b 1
    )
) else (
    echo -- Looking in local packages
)

if "%USE_VCPKG%"=="true" (
    (
        cmake -S . -B "%BUILD_DIR%" ^
            -DCMAKE_TOOLCHAIN_FILE="%VCPKG_DIR%\scripts\buildsystems\vcpkg.cmake" ^
            -DVCPKG_TARGET_TRIPLET=%TRIPLET% ^
            -DAPP_NAME="vsna"
        cmake --build "%BUILD_DIR%" --config Debug
    )
) else (
    (
        cmake -S . -B "%BUILD_DIR%" ^
            -DVCPKG_TARGET_TRIPLET=%TRIPLET% ^
            -DAPP_NAME="vsna"
        cmake --build "%BUILD_DIR%" --config Debug
    )
)

echo -- Build done!
exit /b 0


:cmd_clean
if exist "%OUT_DIR%" (
    rd /s /q "%OUT_DIR%"
    echo -- Removed %OUT_DIR%
)
echo -- Clean done!
exit /b 0


:parse_loop
if "%~1"=="" goto parse_done

if /I "%~1"=="--clean" (
    call :cmd_clean
    popd
    exit /b 0
) else if /I "%~1"=="-h" (
    call :usage
    popd
    exit /b 0
) else if /I "%~1"=="--help" (
    call :usage
    popd
    exit /b 0
) else if /I "%~1"=="--no-vcpkg" (
    set "USE_VCPKG=false"
) else (
    echo Unknown arg: %~1
    popd
    exit /b 1
)

shift
goto parse_loop


:parse_done
call :cmd_build
popd
exit /b 0

:cmd_end
popd
exit /b 0